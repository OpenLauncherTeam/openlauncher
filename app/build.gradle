apply plugin: 'com.android.application'
apply plugin: 'android-apt'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    def VERSION_CODE = 11
    def VERSION_NAME = "0.3.2.1"
    def APP_NAME = 'OpenLauncher'
    def APP_PACKAGE = "com.benny.openlauncher"
    def APP_ICON ="@drawable/ic_launcher"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        multiDexEnabled false

        buildConfigField "boolean", "IS_TEST_BUILD", "false"
        buildConfigField "boolean", "IS_GPLAY_BUILD", "false"
    }

    productFlavors {
        flavorDefault {
            applicationId APP_PACKAGE
            resValue 'string', 'app_name', APP_NAME
            manifestPlaceholders = [appIcon: APP_ICON]
            versionCode VERSION_CODE ; versionName VERSION_NAME
        }

        flavorTest {
            applicationId APP_PACKAGE + ".test"
            resValue 'string', 'app_name', '"OL Nightly"'
            manifestPlaceholders = [appIcon: APP_ICON + "_test"]
            versionCode = Integer.parseInt(new Date().format('yyMMdd'))
            versionName = new Date().format('yyMMdd')
            buildConfigField "boolean", "IS_TEST_BUILD", "true"
        }

        flavorGplay {
            applicationId APP_PACKAGE
            resValue 'string', 'app_name', APP_NAME
            manifestPlaceholders = [appIcon: APP_ICON]
            versionCode VERSION_CODE ; versionName VERSION_NAME
            buildConfigField "boolean", "IS_GPLAY_BUILD", "true"
        }
    }

    lintOptions {
        disable 'MissingTranslation'
        disable 'ExtraTranslation'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    // Support libraries
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.android.support:support-v13:25.3.1'

    // UI Libs
    compile('com.mikepenz:fastadapter:2.1.7@aar') { transitive = true }
    compile('com.github.ozodrukh:CircularReveal:2.0.1@aar') { transitive = true; }
    compile 'com.mikepenz:fastadapter-commons:2.1.0@aar'
    compile 'com.mikepenz:fastadapter-extensions:2.1.7@aar'
    compile 'com.github.turing-tech:MaterialScrollBar:12.3.5'
    compile 'com.github.daniel-stoneuk:material-about-library:1.4.2'
    compile 'com.afollestad.material-dialogs:core:0.9.3.0'
    compile 'com.balysv:material-ripple:1.0.2'
    compile 'net.qiujuer.genius:blur:2.0.0'
    compile 'in.championswimmer:SimpleFingerGestures_Android_Library:1.2'
    compile 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
    compile 'com.github.chyrta:AndroidOnboarder:0.6'
    compile 'cat.ereza:customactivityoncrash:1.5.0'

    // Tools
    compile 'com.jakewharton:butterknife:8.0.1'
    compile 'com.google.code.gson:gson:2.8.0'

    // Compile project libraries
    compile project(':app:libs:materialpreffragment')

    // Android-Apt
    apt 'com.jakewharton:butterknife-compiler:8.0.1'
}